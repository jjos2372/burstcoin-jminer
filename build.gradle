buildscript {
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.4'
    }

    repositories {
        mavenCentral()
        jcenter()
        maven { url 'https://plugins.gradle.org/m2/' }
    }
}

plugins {
    id "distribution"
    id 'com.palantir.git-version' version '0.12.2'
    id 'edu.sc.seis.launch4j' version '2.4.6'
}

repositories {
    mavenCentral()
    jcenter()
    maven { url 'https://jitpack.io' }
}

apply plugin: 'java'
apply plugin: 'com.github.johnrengelman.shadow'

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
  implementation 'org.springframework.boot:spring-boot-starter:2.1.18.RELEASE'
  implementation 'org.springframework.boot:spring-boot-starter-jetty:2.1.18.RELEASE'
  
  implementation 'com.github.signum-network:signumj:abaddd6ecd'
  implementation 'com.fasterxml.jackson.core:jackson-core:2.12.4'
  implementation 'com.fasterxml.jackson.core:jackson-databind:2.12.4'
  implementation 'com.fasterxml.jackson.core:jackson-annotations:2.12.4'
  
  implementation 'org.jocl:jocl-blast:1.5.2'
  implementation 'net.smacke:jaydio:0.1'
  
  
}

def details = versionDetails()

task versionProp()  {
    doFirst {
        new File(sourceSets.main.output.resourcesDir, "/version.properties").text = "version:${details.lastTag}"
    }
}

task buildJar(dependsOn: shadowJar)

jar {
    manifest {
        attributes 'Main-Class': 'signum.jminer.JMinerApplication'
    }
}

shadowJar.dependsOn versionProp

createExe {
    outfile = 'jminer.exe'
    // icon = "${projectDir}/dist/jminer.ico"
    jreMinVersion = '1.8.0'
    copyright = 'signum.network'
    companyName = 'https://signum.network'
    textVersion = details.lastTag
    dontWrapJar = 'true'
    
    mainClassName = 'signum.jminer.JMinerApplication'
    copyConfigurable = project.tasks.shadowJar.outputs.files
    jar = "lib/${project.tasks.shadowJar.archiveName}"
}

distributions {
    main {
        distributionBaseName = "signum-jminer-${details.lastTag}"
        contents {
            from 'jminer.default.properties'
            from 'LICENSE.md'
            from 'README.md'
            from project.tasks.shadowJar.outputs.files
            from "${project.buildDir}/launch4j/jminer.exe"
            from 'jminer.sh'
            
            rename '(.+).default(.+)', '$1$2'
        }
    }
}
